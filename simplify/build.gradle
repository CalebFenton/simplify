buildscript {
    dependencies {
        classpath depends.proguard_gradle
    }
    repositories {
        mavenCentral()
    }
}

dependencies {
    compile project(':smalivm')

    // Lazy
    compile depends.commons_io
    compile depends.commons_lang

    // CLI parsing
    compile depends.args4j

    // Primitive collections
    compile depends.trove4j

    // Perfromance benchmarking
    compile depends.perfidix

    // Logging
    compile depends.slf4j_api
    compile 'ch.qos.logback:logback-core:1.1.3'
    compile 'ch.qos.logback:logback-classic:1.1.3'

    // Testing
    testCompile depends.junit
    testCompile depends.mockito
    testCompile project(path: ':smalivm', configuration: 'testArtifacts')
}

// Build a separate jar that contains all dependencies
task fatJar(type: Jar) {
    from sourceSets.main.output
    from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }

    manifest {
        attributes('Main-Class': 'org.cf.simplify.Main')
    }

    doLast {
        if (!System.getProperty('os.name').toLowerCase().contains('windows')) {
            ant.symlink(link: file("${destinationDir}/simplify.jar"), resource: archivePath, overwrite: true)
        }
    }
}
tasks.getByPath('build').dependsOn(fatJar)

task proguard(type: proguard.gradle.ProGuardTask, dependsOn: fatJar) {
    def outFile = fatJar.destinationDir.getPath() + '/' + fatJar.baseName + '-' + fatJar.version + '-proguard' + '.' + fatJar.extension

    print fatJar.archivePath
    injars fatJar.archivePath
    outjars outFile

    libraryjars "${System.properties['java.home']}/lib/rt.jar"

    dontobfuscate
    //dontoptimize

    keepclassmembers 'enum * { public static **[] values(); public static ** valueOf(java.lang.String); }'
    keepattributes '*Annotation*,Signature,InnerClasses,EnclosingMethod'

    keep 'public class org.cf.simplify.Main { public static void main(java.lang.String[]); }'
    keep 'public class org.cf.simplify.Options  { *; }'
    keep 'public class android.util.**  { *; }'

    keep 'class org.apache.commons.logging.** { public protected *; }'
    keep 'public class org.kohsuke.args4j.** { public protected *; }'
    keep 'public class ch.qos.** { public protected *; }'

    dontwarn
    verbose
}
